" Name: Arnold Chand
" Github: https://github.com/creativenull
" Description: My vimrc, currently tested on a Linux machine.
" Requires:
"   + git
"   + curl
"   + python3
"   + ripgrep
"
" =============================================================================
" = User Initialize (USER) =
" =============================================================================

let mapleader = "\<Space>"

let g:user = {}
let g:user.enable_transparent = v:false

" Vim filepaths
" ---

let g:user.config = {}

if has('win32')
  let g:user.config.cachedir = expand('$HOME/AppData/Local/Temp/vim')
  let g:user.config.configdir = expand('$HOME')
  let g:user.config.datadir = expand('$HOME/vimfiles')
else
  let g:user.config.cachedir = expand('$HOME/.cache/vim')
  let g:user.config.configdir = expand('$HOME')
  let g:user.config.datadir = expand('$HOME/.vim')
endif

let g:user.config.undodir = printf('%s/undo', g:user.config.cachedir)

" Enable vim features
" ---

unlet! skip_defaults_vim
source $VIMRUNTIME/defaults.vim

filetype plugin indent on
syntax on

" Hard dependencies
" ---

" Pre-Requisites
let s:exec_list = ['git', 'curl', 'python3', 'rg', 'deno']

for s:exec in s:exec_list
  if !executable(s:exec)
    echoerr printf('[vim] `%s` is needed!', s:exec)

    finish
  endif
endfor

" OS Specific options
" ---

" For Windows
if has('win32')
  if !executable('pwsh')
    echoerr '[vim] PowerShell Core >= v6 is required!'

    finish
  endif

  let s:shcmd_flag = [
    \ '-NoLogo',
    \ '-NoProfile',
    \ '-ExecutionPolicy',
    \ 'RemoteSigned',
    \ '-Command',
    \ '[Console]::InputEncoding=[Console]::OutputEncoding=[System.Text.Encoding]::UTF8;',
  \ ]

  let &shellcmdflag = join(s:shcmd_flag, ' ')
  let &shellredir = '2>&1 | Out-File -Encoding UTF8 %s; exit $LastExitCode'
  let &shellpipe = '2>&1 | Out-File -Encoding UTF8 %s; exit $LastExitCode'
  set shell=pwsh
  set shellquote=
  set shellxquote=
endif

" =============================================================================
" = User Commands (CMD) =
" =============================================================================

command! Config edit $MYVIMRC
command! ConfigReload source $MYVIMRC | nohlsearch

command! ToggleConcealLevel call user#utils#ToggleConcealLevel()
command! ToggleCodeshot call user#utils#ToggleCodeshot()

command! MyTodoPersonal edit ~/todofiles/personal/README.md
command! MyTodoWork edit ~/todofiles/work/README.md

" Command Abbreviations, I can't release my shift key fast enough ðŸ˜­
cnoreabbrev Q  q
cnoreabbrev Qa qa
cnoreabbrev W  w
cnoreabbrev Wq wq

" =============================================================================
" = Autocmds (AUG) =
" =============================================================================

function! s:set_transparent_highlights() abort
  if g:user.enable_transparent
    " Core highlights to make transparent
    highlight Normal cterm=NONE ctermbg=NONE gui=NONE guibg=NONE
    highlight SignColumn cterm=NONE ctermbg=NONE gui=NONE guibg=NONE
    highlight LineNr gui=NONE guibg=NONE guifg=#EEEEEE
    highlight CursorLineNr cterm=NONE ctermbg=NONE gui=NONE guibg=NONE
    highlight EndOfBuffer cterm=NONE ctermbg=NONE gui=NONE guibg=NONE
    highlight Visual cterm=NONE gui=NONE guibg=#555555

    " Make tabline bg tranparent
    highlight TabLineFill cterm=NONE ctermbg=NONE gui=NONE guibg=NONE
    highlight TabLine cterm=NONE ctermbg=NONE gui=NONE guibg=NONE

    " Float Border
    highlight NormalFloat cterm=NONE ctermbg=NONE gui=NONE guibg=NONE
    highlight FloatBorder cterm=NONE ctermbg=NONE gui=NONE guibg=NONE guifg=#EEEEEE

    " Vertical Line
    highlight ColorColumn cterm=NONE gui=NONE guibg=#999999
  endif
endfunction

augroup transparent_user_events
  autocmd!
  autocmd ColorScheme * call s:set_transparent_highlights()
augroup END

augroup customhl_user_events
  autocmd!
  " Different color when confirming selected substitution with `:s`
  autocmd ColorScheme * highlight IncSearch gui=NONE guibg=#103DA5 guifg=#eeeeee
augroup END

augroup custom_indent_user_events
  autocmd!
  autocmd FileType javascript,javascriptreact call user#utils#IndentSize(2, v:true)
  autocmd FileType json,jsonc call user#utils#IndentSize(2, v:true)
  autocmd FileType markdown call user#utils#IndentSize(4, v:true) | setlocal spell iskeyword+=-
  autocmd FileType php,blade,html call user#utils#IndentSize(4, v:true) | setlocal iskeyword+=-
  autocmd FileType scss,sass,css call user#utils#IndentSize(2, v:true)
  autocmd FileType typescript,typescriptreact call user#utils#IndentSize(2, v:true)
  autocmd FileType vim,lua call user#utils#IndentSize(2, v:true)
  autocmd FileType vue call user#utils#IndentSize(2, v:true) | setlocal iskeyword+=-
augroup END

" =============================================================================
" = Options (OPT) =
" =============================================================================

" Ensure dir for storing undo changes
if !isdirectory(g:user.config.undodir)
  if has('win32')
    execute printf('silent !mkdir -Recurse %s', g:user.config.undodir)
  else
    execute printf('silent !mkdir -p %s', g:user.config.undodir)
  endif
endif

" Completion
set completeopt=menuone,noinsert,noselect
set shortmess+=c
set wildmenu
set wildoptions=pum,fuzzy

" Search
set hlsearch
set ignorecase
set incsearch
set path=**
set showmatch
set smartcase

" Editor
set autoindent
set colorcolumn=120
set expandtab
set lazyredraw
set nofoldenable
set nospell
set nowrap
set scrolloff=1
set shiftwidth=4
set smartindent
set smarttab
set softtabstop=4
set tabstop=8
set wildignorecase

" System
let &undodir=g:user.config.undodir
set backspace=indent,eol,start
set encoding=utf-8
set history=10000
set nobackup
set noswapfile
set ttimeoutlen=50
set undofile
set undolevels=10000
set updatetime=250
set clipboard-=autoselect

if user#utils#IsWSL()
  set mouse=
endif

" UI
set cmdheight=2
set hidden
set laststatus=2
set number
set showtabline=2
set signcolumn=yes

if has('termguicolors')
  set termguicolors
  let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
  let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
endif

" =============================================================================
" = Keybindings (KEY) =
" =============================================================================

" Unbind default bindings for arrow keys, trust me this is for your own good
noremap  <Up>    <Nop>
noremap  <Down>  <Nop>
noremap  <Left>  <Nop>
noremap  <Right> <Nop>
inoremap <Up>    <Nop>
inoremap <Down>  <Nop>
inoremap <Left>  <Nop>
inoremap <Right> <Nop>

" Resize window panes, we can use those arrow keys
" to help use resize windows - at least we give them some purpose
nnoremap <Up>    <Cmd>resize +2<CR>
nnoremap <Down>  <Cmd>resize -2<CR>
nnoremap <Left>  <Cmd>vertical resize -2<CR>
nnoremap <Right> <Cmd>vertical resize +2<CR>

" Map Esc, to perform quick switching between Normal and Insert mode
inoremap jk <Esc>

" Map escape from terminal input to Normal mode
tnoremap <Esc> <C-\><C-n>
tnoremap <C-[> <C-\><C-n>

" Disable highlights
nnoremap <Leader><CR> <Cmd>noh<CR>

" List all buffers
nnoremap <Leader>bb <Cmd>buffers<CR>

" Go to next buffer
nnoremap <C-l> <Cmd>bnext<CR>
nnoremap <Leader>bn <Cmd>bnext<CR>

" Go to previous buffer
nnoremap <C-h> <Cmd>bprevious<CR>
nnoremap <Leader>bp <Cmd>bprevious<CR>

" Close the current buffer, and more?
nnoremap <Leader>bd <Cmd>bp<Bar>sp<Bar>bn<Bar>bd<CR>

" Close all buffer, except current
nnoremap <Leader>bx <Cmd>%bd<Bar>e#<Bar>bd#<CR>

" Edit vimrc
nnoremap <Leader>ve <Cmd>edit $MYVIMRC<CR>

" Source the vimrc to reflect changes
nnoremap <Leader>vs <Cmd>ConfigReload<CR>

" Reload file
nnoremap <Leader>r <Cmd>edit!<CR>

" List all maps
nnoremap <Leader>mn <Cmd>nmap<CR>
nnoremap <Leader>mv <Cmd>vmap<CR>
nnoremap <Leader>mi <Cmd>imap<CR>
nnoremap <Leader>mt <Cmd>tmap<CR>
nnoremap <Leader>mc <Cmd>cmap<CR>

" Move lines up/down with alt+j/k
set <M-j>=j
set <M-k>=k
nnoremap <M-j> :m .+1<CR>==
nnoremap <M-k> :m .-2<CR>==
vnoremap <M-j> :m '>+1<CR>gv=gv
vnoremap <M-k> :m '<-2<CR>gv=gv

" Copy/Paste system clipboard
nnoremap <Leader>y "+y
nnoremap <Leader>p "+p
nnoremap Y y$

" Format entire file with =
nnoremap g= gg=G

" Execute last recorded macro
nnoremap Q @@

" =============================================================================
" = Plugin Manager (PLUG) =
" =============================================================================

function! PackagerInit() abort
  packadd vim-packager
  call packager#init()

  " Self-care
  call packager#add('kristijanhusak/vim-packager', #{ type: 'opt' })

  " Plugin dependencies {{{
  call packager#add('Shougo/context_filetype.vim')
  call packager#add('lambdalisue/nerdfont.vim')
  call packager#add('vim-denops/denops.vim')
  " }}}

  " Core {{{
  call packager#add('cohama/lexima.vim')
  call packager#add('creativenull/projectlocal-vim')
  call packager#add('editorconfig/editorconfig-vim')
  call packager#add('junegunn/vim-easy-align')
  call packager#add('mattn/emmet-vim')
  call packager#add('tpope/vim-abolish')
  call packager#add('tpope/vim-repeat')
  call packager#add('tpope/vim-surround')
  call packager#add('tyru/caw.vim')
  " }}}

  " File Explorer {{{
  call packager#add('lambdalisue/fern-renderer-nerdfont.vim')
  call packager#add('lambdalisue/fern.vim')
  call packager#add('lambdalisue/nerdfont.vim')
  " }}}

  " Fuzzy Finder {{{
  call packager#add('junegunn/fzf')
  call packager#add('junegunn/fzf.vim')
  " }}}

  " Git {{{
  call packager#add('airblade/vim-gitgutter')
  call packager#add('itchyny/vim-gitbranch')
  call packager#add('lambdalisue/gin.vim')
  " }}}

  " Linter and formatter configurations {{{
  call packager#add('dense-analysis/ale')
  " }}}

  " Auto-completion + LSP client {{{
  " call packager#add('neoclide/coc.nvim', #{ branch: 'release' })
  " }}}

  " LSP client {{{
  call packager#add('prabirshrestha/vim-lsp')
  call packager#add('rhysd/vim-lsp-ale')
  " }}}

  " Auto-completion {{{
  call packager#add('prabirshrestha/asyncomplete.vim')
  call packager#add('prabirshrestha/asyncomplete-lsp.vim')
  call packager#add('prabirshrestha/asyncomplete-buffer.vim')
  " }}}

  " Auto-completion {{{
  " call packager#add('Shougo/ddc.vim')
  " call packager#add('tani/ddc-fuzzy')
  " call packager#add('shun/ddc-vim-lsp')
  " call packager#add('Shougo/ddc-around')
  " call packager#add('matsui54/ddc-buffer')
  " }}}

  " File syntax plugins {{{
  call packager#add('MaxMEllon/vim-jsx-pretty')
  call packager#add('elzr/vim-json')
  call packager#add('kevinoid/vim-jsonc')
  call packager#add('junegunn/vader.vim')
  call packager#add('jwalton512/vim-blade')
  call packager#add('pangloss/vim-javascript')
  call packager#add('posva/vim-vue')
  call packager#add('MTDL9/vim-log-highlighting')
  call packager#add('lacygoill/vim9-syntax')
  " }}}

  " UI {{{
  call packager#add('Yggdroot/indentLine')
  call packager#add('machakann/vim-highlightedyank')
  call packager#add('itchyny/lightline.vim')
  call packager#add('mengelbrecht/lightline-bufferline')
  call packager#add('markonm/traces.vim')
  " }}}

  " Colorschemes {{{
  call packager#add('bluz71/vim-moonfly-colors')
  call packager#add('bluz71/vim-nightfly-guicolors')
  call packager#add('fnune/base16-vim')
  call packager#add('keqizeng/nightelf')
  call packager#add('nanotech/jellybeans.vim')
  " }}}
endfunction

command! -nargs=* -bar PackagerInstall call PackagerInit() | call packager#install(<args>)
command! -nargs=* -bar PackagerUpdate call PackagerInit() | call packager#update(<args>)
command! -bar PackagerClean call PackagerInit() | call packager#clean()
command! -bar PackagerStatus call PackagerInit() | call packager#status()

call user#packager#Bootstrap()

" =============================================================================
" = Plugin Configurations (POST) =
" =============================================================================

" gin.vim Config
" ---
call user#gin#Setup()

" Emmet Config
" ---
call user#emmet#Setup()

" fern.vim Config
" ---
call user#fern#Setup()

" fzf.vim Config
" ---
call user#fzf#Setup()

" vim-gitgutter Config
" ---
let g:gitgutter_map_keys = 0

" ale Config
" ---
call user#ale#Setup()

" coc.nvim Config
" ---
" call user#coc#Setup()

" vim-vue Config
" ---
let g:vue_pre_processors = []

" vim-json Config
" ---
let g:vim_json_syntax_conceal = 0

" indentLine Config
" ---
let g:indentLine_char = 'â”‚'

" vim-highlightedyank Config
" ---
call user#highlightedyank#Setup()

" lightline.vim Config
" ---
call user#lightline#Setup()

" gitgutter Config
" ---
call user#gitgutter#Setup()

" asyncomplete.vim Config
" ---
call user#asyncomplete#Setup()

" ddc.vim Config
" ---
" call user#ddc#Setup()

" vim-lsp Config
" ---
call user#lsp#Setup()

" =============================================================================
" = UI/Theme (THEME) =
" =============================================================================

let g:moonflyNormalFloat = 1
let g:moonflyTransparent = 1

set background=dark
colorscheme moonfly
