" Name: Arnold Chand
" Github: https://github.com/creativenull
" Description: My vimrc, currently tested on a Linux machine.
" Requires:
"   + git
"   + curl
"   + python3
"   + ripgrep
"
" =============================================================================
" = User Initialize (USER) =
" =============================================================================

if !has('vim9script')
  echoerr '[vim] Vim 9 is required for this config!'
  finish
endif

let mapleader = "\<Space>"

let g:user = {}
let g:user.enable_transparent = v:false

" Vim filepaths
" ---

let g:user.config = {}

if has('win32')
  let g:user.config.cachedir = expand('$HOME/AppData/Local/Temp/vim')
  let g:user.config.configdir = expand('$HOME')
  let g:user.config.datadir = expand('$HOME/vimfiles')
else
  let g:user.config.cachedir = expand('$HOME/.cache/vim')
  let g:user.config.configdir = expand('$HOME')
  let g:user.config.datadir = expand('$HOME/.vim')
endif

let g:user.config.undodir = printf('%s/undo', g:user.config.cachedir)

" Enable vim features
" ---

unlet! skip_defaults_vim
source $VIMRUNTIME/defaults.vim

filetype plugin indent on
syntax on

" Hard dependencies
" ---

" Pre-Requisites
let s:exec_list = ['git', 'curl', 'python3', 'rg', 'deno']

for s:exec in s:exec_list
  if !executable(s:exec)
    echoerr printf('[vim] `%s` is needed!', s:exec)
    finish
  endif
endfor

" OS Specific options
" ---

" For Windows
if has('win32')
  if !executable('pwsh')
    echoerr '[vim] PowerShell Core >= v6 is required!'
    finish
  endif

  let s:shcmd_flag = [
    \ '-NoLogo',
    \ '-NoProfile',
    \ '-ExecutionPolicy',
    \ 'RemoteSigned',
    \ '-Command',
    \ '[Console]::InputEncoding=[Console]::OutputEncoding=[System.Text.Encoding]::UTF8;',
  \ ]

  let &shellcmdflag = join(s:shcmd_flag, ' ')
  let &shellredir = '2>&1 | Out-File -Encoding UTF8 %s; exit $LastExitCode'
  let &shellpipe = '2>&1 | Out-File -Encoding UTF8 %s; exit $LastExitCode'
  set shell=pwsh
  set shellquote=
  set shellxquote=
endif

" =============================================================================
" = User Commands (CMD) =
" =============================================================================

command! Config edit $MYVIMRC
command! ConfigReload source $MYVIMRC | nohlsearch

command! ToggleConcealLevel call user#utils#ToggleConcealLevel()
command! ToggleCodeshot call user#utils#ToggleCodeshot()

command! MyTodoPersonal edit ~/todofiles/personal/README.md
command! MyTodoWork edit ~/todofiles/work/README.md

" Command Abbreviations, I can't release my shift key fast enough ðŸ˜­
cnoreabbrev Q  q
cnoreabbrev Qa qa
cnoreabbrev W  w
cnoreabbrev Wq wq

" =============================================================================
" = Autocmds (AUG) =
" =============================================================================

function! s:set_transparent_highlights() abort
  if g:user.enable_transparent
    " Core highlights to make transparent
    highlight Normal cterm=NONE ctermbg=NONE gui=NONE guibg=NONE
    highlight SignColumn cterm=NONE ctermbg=NONE gui=NONE guibg=NONE
    highlight LineNr gui=NONE guibg=NONE guifg=#EEEEEE
    highlight CursorLineNr cterm=NONE ctermbg=NONE gui=NONE guibg=NONE
    highlight EndOfBuffer cterm=NONE ctermbg=NONE gui=NONE guibg=NONE
    highlight Visual cterm=NONE gui=NONE guibg=#555555

    " Make tabline bg tranparent
    highlight TabLineFill cterm=NONE ctermbg=NONE gui=NONE guibg=NONE
    highlight TabLine cterm=NONE ctermbg=NONE gui=NONE guibg=NONE

    " Float Border
    highlight NormalFloat cterm=NONE ctermbg=NONE gui=NONE guibg=NONE
    highlight FloatBorder cterm=NONE ctermbg=NONE gui=NONE guibg=NONE guifg=#EEEEEE

    " Vertical Line
    highlight ColorColumn cterm=NONE gui=NONE guibg=#999999
  endif
endfunction

augroup transparent_user_events
  autocmd!
  autocmd ColorScheme * call s:set_transparent_highlights()
augroup END

augroup customhl_user_events
  autocmd!
  " Different color when confirming selected substitution with `:s`
  autocmd ColorScheme * highlight IncSearch gui=NONE guibg=#103DA5 guifg=#eeeeee
augroup END

augroup custom_indent_user_events
  autocmd!
  autocmd FileType javascript,javascriptreact call user#utils#IndentSize(2, v:true)
  autocmd FileType json,jsonc call user#utils#IndentSize(2, v:true)
  autocmd FileType markdown call user#utils#IndentSize(4, v:true) | setlocal spell iskeyword+=-
  autocmd FileType php,blade,html call user#utils#IndentSize(4, v:true) | setlocal iskeyword+=-
  autocmd FileType scss,sass,css call user#utils#IndentSize(2, v:true)
  autocmd FileType typescript,typescriptreact call user#utils#IndentSize(2, v:true)
  autocmd FileType vim,lua call user#utils#IndentSize(2, v:true)
  autocmd FileType vue call user#utils#IndentSize(2, v:true) | setlocal iskeyword+=-
augroup END

" =============================================================================
" = Options (OPT) =
" =============================================================================

" Ensure dir for storing undo changes
if !isdirectory(g:user.config.undodir)
  if has('win32')
    execute printf('silent !mkdir -Recurse %s', g:user.config.undodir)
  else
    execute printf('silent !mkdir -p %s', g:user.config.undodir)
  endif
endif

" Completion
set completeopt=menuone,noinsert,noselect
set shortmess+=c
set wildmenu
set wildoptions=pum,fuzzy

" Search
set hlsearch
set ignorecase
set incsearch
set path=**
set showmatch
set smartcase

" Editor
set autoindent
set colorcolumn=120
set expandtab
set lazyredraw
set nofoldenable
set nospell
set nowrap
set scrolloff=1
set shiftwidth=4
set smartindent
set smarttab
set softtabstop=4
set tabstop=8
set wildignorecase

" System
let &undodir=g:user.config.undodir
set backspace=indent,eol,start
set encoding=utf-8
set history=10000
set nobackup
set noswapfile
set ttimeoutlen=50
set undofile
set undolevels=10000
set updatetime=250
set clipboard-=autoselect

if user#utils#IsWSL()
  set mouse=
endif

" UI
set cmdheight=2
set hidden
set laststatus=2
set number
set showtabline=2
set signcolumn=yes

if has('termguicolors')
  set termguicolors
  let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
  let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
endif

" =============================================================================
" = Keybindings (KEY) =
" =============================================================================

" Unbind default bindings for arrow keys, trust me this is for your own good
noremap  <Up>    <Nop>
noremap  <Down>  <Nop>
noremap  <Left>  <Nop>
noremap  <Right> <Nop>
inoremap <Up>    <Nop>
inoremap <Down>  <Nop>
inoremap <Left>  <Nop>
inoremap <Right> <Nop>

" Resize window panes, we can use those arrow keys
" to help use resize windows - at least we give them some purpose
nnoremap <Up>    <Cmd>resize +2<CR>
nnoremap <Down>  <Cmd>resize -2<CR>
nnoremap <Left>  <Cmd>vertical resize -2<CR>
nnoremap <Right> <Cmd>vertical resize +2<CR>

" Map Esc, to perform quick switching between Normal and Insert mode
inoremap jk <Esc>

" Map escape from terminal input to Normal mode
tnoremap <Esc> <C-\><C-n>
tnoremap <C-[> <C-\><C-n>

" Disable highlights
nnoremap <Leader><CR> <Cmd>noh<CR>

" List all buffers
nnoremap <Leader>bb <Cmd>buffers<CR>

" Go to next buffer
nnoremap <C-l> <Cmd>bnext<CR>
nnoremap <Leader>bn <Cmd>bnext<CR>

" Go to previous buffer
nnoremap <C-h> <Cmd>bprevious<CR>
nnoremap <Leader>bp <Cmd>bprevious<CR>

" Close the current buffer, and more?
nnoremap <Leader>bd <Cmd>bp<Bar>sp<Bar>bn<Bar>bd<CR>

" Close all buffer, except current
nnoremap <Leader>bx <Cmd>%bd<Bar>e#<Bar>bd#<CR>

" Edit vimrc
nnoremap <Leader>ve <Cmd>edit $MYVIMRC<CR>

" Source the vimrc to reflect changes
nnoremap <Leader>vs <Cmd>ConfigReload<CR>

" Reload file
nnoremap <Leader>r <Cmd>edit!<CR>

" List all maps
nnoremap <Leader>mn <Cmd>nmap<CR>
nnoremap <Leader>mv <Cmd>vmap<CR>
nnoremap <Leader>mi <Cmd>imap<CR>
nnoremap <Leader>mt <Cmd>tmap<CR>
nnoremap <Leader>mc <Cmd>cmap<CR>

" Move lines up/down with alt+j/k
set <M-j>=j
set <M-k>=k
nnoremap <M-j> :m .+1<CR>==
nnoremap <M-k> :m .-2<CR>==
vnoremap <M-j> :m '>+1<CR>gv=gv
vnoremap <M-k> :m '<-2<CR>gv=gv

" Copy/Paste system clipboard
vnoremap p "_dP
nnoremap <Leader>y "+y
nnoremap <Leader>p <Cmd>set paste<Bar>"+p<Bar>set nopaste<CR>
nnoremap Y y$

" Format entire file with =
nnoremap g= gg=G

" Execute last recorded macro
nnoremap Q @@

" =============================================================================
" = Plugin Manager (PLUG) =
" =============================================================================

function! PackagerInit() abort
  packadd vim-packager
  call packager#init()

  " Self-care
  call packager#add('kristijanhusak/vim-packager', #{ type: 'opt' })

  " Plugin dependencies {{{
  call packager#add('lambdalisue/nerdfont.vim', #{ tag: 'v1.3.0' })
  call packager#add('vim-denops/denops.vim', #{ tag: 'v3.2.0' })
  " }}}

  " Core {{{
  call packager#add('cohama/lexima.vim', #{ commit: 'fbc05de53ca98b7f36a82f566db1df49864e58ef' })
  call packager#add('creativenull/projectlocal-vim', #{ tag: 'v0.4.3' })
  call packager#add('editorconfig/editorconfig-vim', #{ commit: 'd354117b72b3b43b75a29b8e816c0f91af10efe9' })
  call packager#add('junegunn/vim-easy-align', #{ commit: '12dd6316974f71ce333e360c0260b4e1f81169c3' })
  call packager#add('mattn/emmet-vim', #{ commit: 'def5d57a1ae5afb1b96ebe83c4652d1c03640f4d' })
  call packager#add('tpope/vim-abolish', #{ tag: 'v1.1' })
  call packager#add('tpope/vim-repeat', #{ commit: '24afe922e6a05891756ecf331f39a1f6743d3d5a' })
  call packager#add('tpope/vim-surround', #{ tag: 'v2.2' })
  call packager#add('Shougo/context_filetype.vim', #{ commit: '28768168261bca161c3f2599e0ed63c96aab6dea' })
  call packager#add('tyru/caw.vim', #{ commit: '3aefcb5a752a599a9200dd801d6bcb0b7606bf29' })
  " }}}

  " File Explorer {{{
  call packager#add('lambdalisue/fern.vim', #{ tag: 'v1.51.1' })
  call packager#add('lambdalisue/fern-renderer-nerdfont.vim', #{ commit: '1a3719f226edc27e7241da7cda4bc4d4c7db889c' })
  " }}}

  " Fuzzy Finder {{{
  call packager#add('junegunn/fzf', #{ tag: '0.32.1' })
  call packager#add('junegunn/fzf.vim', #{ commit: 'c491d702b76c6b4918abb80be3cfb57d1b618ffa' })
  " }}}

  " Git {{{
  call packager#add('lambdalisue/gin.vim', #{ tag: 'v0.2.1' })
  call packager#add('itchyny/vim-gitbranch', #{ commit: '1a8ba866f3eaf0194783b9f8573339d6ede8f1ed' })
  call packager#add('airblade/vim-gitgutter', #{ commit: 'f19b6203191d69de955d91467a5707959572119b' })
  " }}}

  " Linter and formatter configurations {{{
  call packager#add('dense-analysis/ale', #{ tag: 'v3.2.0' })
  " }}}

  " Auto-completion + LSP client {{{
  " call packager#add('neoclide/coc.nvim', #{ branch: 'release' })
  " }}}

  " LSP client {{{
  call packager#add('prabirshrestha/vim-lsp', #{ commit: '4d8a27513daef41915eaf2d315a2662e95d71080' })
  call packager#add('rhysd/vim-lsp-ale', #{ commit: 'db0f9a8a33c0480988dc420cd2fba8a07743e4a4' })
  " }}}

  " Auto-completion {{{
  call packager#add('Shougo/ddc.vim', #{ tag: 'v2.4.0' })
  call packager#add('tani/ddc-fuzzy', #{ commit: '3339deacff797cc23f79a45c5e72ba0eed0af119' })
  call packager#add('matsui54/denops-signature_help', #{ commit: '807226cf698c537c335284f5a9af79067991f930' })
  call packager#add('shun/ddc-vim-lsp', #{ commit: '8035fe3f7cccbb75686c27e6631361650456d876' })
  call packager#add('Shougo/ddc-around', #{ commit: '58249707478dbd53bd4eb18c0c42770886a849d7' })
  call packager#add('matsui54/ddc-buffer', #{ commit: '5cce1b264d83d6cc38c0bbb044da51ec0dd92c12' })
  " }}}

  " UI {{{
  call packager#add('itchyny/lightline.vim', #{ commit: 'b02ef0d9f253dfc1cbb3f340b74998d7a4db0bf6' })
  call packager#add('mengelbrecht/lightline-bufferline', #{ commit: '8b6e29e65e9711b75df289879186ff3888feed00' })
  call packager#add('Yggdroot/indentLine', #{ commit: 'd15d63bf9c4a74a02470d4bc8ecce53df13e3a75' })
  call packager#add('machakann/vim-highlightedyank', #{ commit: 'f9db473137ca96c6a989ec3e2b7edf8a3189c448' })
  call packager#add('markonm/traces.vim', #{ commit: '9663fcf84de5776bee71b6c816c25ccb6ea11d1a' })
  " }}}

  " File syntax plugins {{{
  call packager#add('elzr/vim-json')
  call packager#add('kevinoid/vim-jsonc')
  call packager#add('junegunn/vader.vim')
  call packager#add('jwalton512/vim-blade')
  call packager#add('pangloss/vim-javascript')
  call packager#add('MaxMEllon/vim-jsx-pretty')
  call packager#add('posva/vim-vue')
  call packager#add('MTDL9/vim-log-highlighting')
  call packager#add('lacygoill/vim9-syntax')
  " }}}

  " Colorschemes {{{
  call packager#add('bluz71/vim-moonfly-colors')
  call packager#add('bluz71/vim-nightfly-guicolors')
  call packager#add('fnune/base16-vim')
  call packager#add('keqizeng/nightelf')
  call packager#add('nanotech/jellybeans.vim')
  " }}}
endfunction

command! -nargs=* -bar PackagerInstall call PackagerInit() | call packager#install(<args>)
command! -nargs=* -bar PackagerUpdate call PackagerInit() | call packager#update(<args>)
command! -bar PackagerClean call PackagerInit() | call packager#clean()
command! -bar PackagerStatus call PackagerInit() | call packager#status()

call user#packager#Bootstrap()

" =============================================================================
" = Plugin Configurations (POST) =
" =============================================================================

" gin.vim Config
" ---
call user#gin#Setup()

" Emmet Config
" ---
call user#emmet#Setup()

" fern.vim Config
" ---
call user#fern#Setup()

" fzf.vim Config
" ---
call user#fzf#Setup()

" vim-gitgutter Config
" ---
let g:gitgutter_map_keys = 0

" ale Config
" ---
call user#ale#Setup()

" coc.nvim Config
" ---
" call user#coc#Setup()

" vim-vue Config
" ---
let g:vue_pre_processors = []

" vim-json Config
" ---
let g:vim_json_syntax_conceal = 0

" indentLine Config
" ---
let g:indentLine_char = 'â”‚'

" vim-highlightedyank Config
" ---
call user#highlightedyank#Setup()

" lightline.vim Config
" ---
call user#lightline#Setup()

" gitgutter Config
" ---
call user#gitgutter#Setup()

" asyncomplete.vim Config
" ---
" call user#asyncomplete#Setup()

" ddc.vim Config
" ---
call user#ddc#Setup()

" vim-lsp Config
" ---
call user#lsp#Setup()

" =============================================================================
" = UI/Theme (THEME) =
" =============================================================================

let g:moonflyNormalFloat = 1
let g:moonflyTransparent = 1

set background=dark
colorscheme moonfly
