" Name: Arnold Chand
" Github: https://github.com/creativenull
" Description: My vimrc, currently tested on a Linux machine.
" Requires:
"   + git
"   + curl
"   + python3
"   + ripgrep
"
" =============================================================================
" = User Initialize (USER) =
" =============================================================================

let mapleader = "\<Space>"

let s:user = {}
let s:user.enable_transparent = v:true

" Vim filepaths
" ---

let s:user.config = {}

if has('win32')
  let s:user.config.cachedir = expand('$HOME/AppData/Local/Temp/vim')
  let s:user.config.configdir = expand('$HOME')
  let s:user.config.datadir = expand('$HOME/vimfiles')
else
  let s:user.config.cachedir = expand('$HOME/.cache/vim')
  let s:user.config.configdir = expand('$HOME')
  let s:user.config.datadir = expand('$HOME/.vim')
endif

let s:user.config.undodir = printf('%s/undo', s:user.config.cachedir)

" Enable vim features
" ---

unlet! skip_defaults_vim
source $VIMRUNTIME/defaults.vim

filetype plugin indent on
syntax on

" Hard dependencies
" ---

" Pre-Requisites
let s:exec_list = ['git', 'curl', 'python3', 'rg', 'node', 'deno']
for s:exec in s:exec_list
  if !executable(s:exec)
    echoerr printf('[vim] `%s` is needed!', s:exec)

    finish
  endif
endfor

" OS Specific options
" ---

" For Windows
if has('win32')
  if !executable('pwsh')
    echoerr '[vim] PowerShell Core >= v6 is required!'

    finish
  endif

  let s:shcmd_flag = [
    \ '-NoLogo',
    \ '-NoProfile',
    \ '-ExecutionPolicy',
    \ 'RemoteSigned',
    \ '-Command',
    \ '[Console]::InputEncoding=[Console]::OutputEncoding=[System.Text.Encoding]::UTF8;',
  \ ]

  let &shellcmdflag = join(s:shcmd_flag, ' ')
  let &shellredir = '2>&1 | Out-File -Encoding UTF8 %s; exit $LastExitCode'
  let &shellpipe = '2>&1 | Out-File -Encoding UTF8 %s; exit $LastExitCode'
  set shell=pwsh
  set shellquote=
  set shellxquote=
endif

" =============================================================================
" = User Commands (CMD) =
" =============================================================================

command! Config edit $MYVIMRC
command! ConfigReload source $MYVIMRC | nohlsearch

command! ToggleConcealLevel call cnull#utils#ToggleConcealLevel()
command! ToggleCodeshot call cnull#utils#ToggleCodeshot()

command! MyTodoPersonal edit ~/todofiles/personal/README.md
command! MyTodoWork edit ~/todofiles/work/README.md

" Command Abbreviations, I can't release my shift key fast enough ðŸ˜­
cnoreabbrev Q  q
cnoreabbrev Qa qa
cnoreabbrev W  w
cnoreabbrev Wq wq

" =============================================================================
" = Autocmds (AUG) =
" =============================================================================

function! s:set_transparent_highlights() abort
  if s:user.enable_transparent
    " Core highlights to make transparent
    highlight Normal gui=NONE guibg=NONE
    highlight SignColumn gui=NONE guibg=NONE
    highlight LineNr gui=NONE guibg=NONE guifg=#888888
    highlight CursorLineNr gui=NONE guibg=NONE
    highlight EndOfBuffer gui=NONE guibg=NONE
    highlight Visual gui=NONE guibg=#555555

    " Sometimes comments are too dark, affects in tranparent mode
    highlight Comment gui=NONE cterm=NONE guibg=NONE guifg=#888888

    " Make tabline bg tranparent
    highlight TabLineFill gui=NONE guibg=NONE
    highlight TabLine gui=NONE guibg=NONE

    " Float Border
    highlight NormalFloat gui=NONE guibg=NONE
    highlight FloatBorder gui=NONE guibg=NONE guifg=#eeeeee

    " Vertical Line
    highlight ColorColumn gui=NONE guibg=#999999
  endif
endfunction

augroup transparent_user_events
  autocmd!

  autocmd ColorScheme * call s:set_transparent_highlights()
augroup END

augroup customhl_user_events
  autocmd!

  " Different color when confirming selected substitution with `:s`
  autocmd ColorScheme * highlight IncSearch gui=NONE guibg=#103DA5 guifg=#eeeeee
augroup END

" =============================================================================
" = Options (OPT) =
" =============================================================================

" Ensure dir for storing undo changes
if !isdirectory(s:user.config.undodir)
  if has('win32')
    execute printf('silent !mkdir -Recurse %s', s:user.config.undodir)
  else
    execute printf('silent !mkdir -p %s', s:user.config.undodir)
  endif
endif

" Completion
set completeopt=menuone,noinsert,noselect
set shortmess+=c
set wildmenu

" Search
set hlsearch
set ignorecase
set incsearch
set path=**
set showmatch
set smartcase

" Editor
set autoindent
set colorcolumn=120
set expandtab
set iskeyword+=-
set lazyredraw
set nofoldenable
set nospell
set nowrap
set scrolloff=3
set shiftwidth=4
set smartindent
set smarttab
set softtabstop=4
set tabstop=8
set wildignorecase

" System
let &undodir=s:user.config.undodir
set backspace=indent,eol,start
set encoding=utf-8
set history=10000
set mouse=a
set nobackup
set noswapfile
set ttimeoutlen=50
set undofile
set undolevels=10000
set updatetime=250

" UI
set cmdheight=2
set hidden
set laststatus=2
set number
set showtabline=2
set signcolumn=yes

if has('termguicolors')
  set termguicolors
  let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
  let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
endif

" =============================================================================
" = Keybindings (KEY) =
" =============================================================================

" Unbind default bindings for arrow keys, trust me this is for your own good
noremap  <Up>    <Nop>
noremap  <Down>  <Nop>
noremap  <Left>  <Nop>
noremap  <Right> <Nop>
inoremap <Up>    <Nop>
inoremap <Down>  <Nop>
inoremap <Left>  <Nop>
inoremap <Right> <Nop>

" Resize window panes, we can use those arrow keys
" to help use resize windows - at least we give them some purpose
nnoremap <Up>    <Cmd>resize +2<CR>
nnoremap <Down>  <Cmd>resize -2<CR>
nnoremap <Left>  <Cmd>vertical resize -2<CR>
nnoremap <Right> <Cmd>vertical resize +2<CR>

" Map Esc, to perform quick switching between Normal and Insert mode
inoremap jk <Esc>

" Map escape from terminal input to Normal mode
tnoremap <Esc> <C-\><C-n>
tnoremap <C-[> <C-\><C-n>

" Disable highlights
nnoremap <Leader><CR> <Cmd>noh<CR>

" List all buffers
nnoremap <Leader>bb <Cmd>buffers<CR>

" Go to next buffer
nnoremap <C-l> <Cmd>bnext<CR>
nnoremap <Leader>bn <Cmd>bnext<CR>

" Go to previous buffer
nnoremap <C-h> <Cmd>bprevious<CR>
nnoremap <Leader>bp <Cmd>bprevious<CR>

" Close the current buffer, and more?
nnoremap <Leader>bd <Cmd>bp<Bar>sp<Bar>bn<Bar>bd<CR>

" Close all buffer, except current
nnoremap <Leader>bx <Cmd>%bd<Bar>e#<Bar>bd#<CR>

" Edit vimrc
nnoremap <Leader>ve <Cmd>edit $MYVIMRC<CR>

" Source the vimrc to reflect changes
nnoremap <Leader>vs <Cmd>ConfigReload<CR>

" Reload file
nnoremap <Leader>r <Cmd>edit!<CR>

" List all maps
nnoremap <Leader>mn <Cmd>nmap<CR>
nnoremap <Leader>mv <Cmd>vmap<CR>
nnoremap <Leader>mi <Cmd>imap<CR>
nnoremap <Leader>mt <Cmd>tmap<CR>
nnoremap <Leader>mc <Cmd>cmap<CR>

" Move lines up/down with alt+j/k
set <M-j>=j
set <M-k>=k
nnoremap <M-j> :m .+1<CR>==
nnoremap <M-k> :m .-2<CR>==
vnoremap <M-j> :m '>+1<CR>gv=gv
vnoremap <M-k> :m '<-2<CR>gv=gv

" Copy/Paste system clipboard
nnoremap <Leader>y "+y
nnoremap <Leader>p "+p
nnoremap Y y$

" Format entire file with =
nnoremap g= gg=G

" Execute last recorded macro
nnoremap Q @@

" =============================================================================
" = Plugin Manager (PLUG) =
" =============================================================================

" Bootstrap - ensure jetpack is downloaded
let s:jetpack_setup = #{
  \ base_dir: expand('~/.vim/pack/jetpack'),
  \ git: 'https://github.com/tani/vim-jetpack.git',
  \ path: expand('~/.vim/pack/jetpack/src/vim-jetpack'),
  \ linkpath: expand('~/.vim/pack/jetpack/opt/vim-jetpack'),
\ }

" Ensure jetpack directories exist
if !isdirectory(s:jetpack_setup.base_dir)
  echo "Creating dirs"

  call system('mkdir -p ' . s:jetpack_setup.base_dir . '/src')
  call system('mkdir -p ' . s:jetpack_setup.base_dir . '/opt')
endif

" Install jetpack and install plugins on first time load
if !isdirectory(s:jetpack_setup.path)
  echo "Downloading vim-jetpack plugin manager"

  let s:gitcmd = ['git', 'clone', '--depth', '1', s:jetpack_setup.git, s:jetpack_setup.path]
  call system(s:gitcmd->join(' '))

  let s:linkcmd = ['ln', '-s', s:jetpack_setup.path, s:jetpack_setup.linkpath]
  call system(s:linkcmd->join(' '))

  autocmd! VimEnter * call jetpack#sync()
endif

" Optimize plugins handled by jetpack
let g:jetpack#optimization = 2

packadd vim-jetpack

call jetpack#begin()

Jetpack 'tani/vim-jetpack', { 'opt': v:true }

" Plugin dependencies {{{
Jetpack 'Shougo/context_filetype.vim'
Jetpack 'vim-denops/denops.vim'
Jetpack 'lambdalisue/nerdfont.vim'
" }}}

" Core {{{
Jetpack 'cohama/lexima.vim'
Jetpack 'godlygeek/tabular'
Jetpack 'tpope/vim-surround'
Jetpack 'tpope/vim-abolish'
Jetpack 'tpope/vim-repeat'
Jetpack 'tyru/caw.vim'
Jetpack 'editorconfig/editorconfig-vim'
Jetpack 'creativenull/projectlocal-vim'
Jetpack 'mattn/emmet-vim' | call cnull#emmet#Setup()
" }}}

" File Explorer {{{
Jetpack 'lambdalisue/fern.vim' | call cnull#fern#Setup()
Jetpack 'lambdalisue/nerdfont.vim'
Jetpack 'lambdalisue/fern-renderer-nerdfont.vim'
" }}}

" Fuzzy Finder {{{
Jetpack 'junegunn/fzf', { 'do': { -> fzf#install() } } | call cnull#fzf#Setup()
Jetpack 'junegunn/fzf.vim'
" }}}

" Snippets {{{
Jetpack 'SirVer/ultisnips'
Jetpack 'honza/vim-snippets'
" }}}

" Git {{{
Jetpack 'tpope/vim-fugitive'
Jetpack 'airblade/vim-gitgutter' | let g:gitgutter_map_keys = 0
" }}}

" Linter and formatter configurations {{{
Jetpack 'dense-analysis/ale' | call cnull#ale#Setup()
" }}}

" Auto completion + LSP client {{{
Jetpack 'neoclide/coc.nvim', { 'branch': 'release' } | call cnull#coc#Setup()
Jetpack 'fannheyward/coc-deno', { 'do': 'yarn install --frozen-lockfile' }
Jetpack 'marlonfan/coc-phpls', { 'do': 'yarn install --frozen-lockfile' }
Jetpack 'neoclide/coc-css', { 'do': 'yarn install --frozen-lockfile' }
Jetpack 'neoclide/coc-html', { 'do': 'yarn install --frozen-lockfile' }
Jetpack 'neoclide/coc-json', { 'do': 'yarn install --frozen-lockfile' }
Jetpack 'neoclide/coc-snippets', { 'do': 'yarn install --frozen-lockfile' }
Jetpack 'neoclide/coc-tsserver', { 'do': 'yarn install --frozen-lockfile' }
Jetpack 'neoclide/coc-vetur', { 'do': 'yarn install --frozen-lockfile' }
Jetpack 'yaegassy/coc-volar', { 'do': 'yarn install --frozen-lockfile' }
" }}}

" File syntax plugins {{{
Jetpack 'pangloss/vim-javascript'
Jetpack 'MaxMEllon/vim-jsx-pretty'
Jetpack 'heavenshell/vim-jsdoc', { 'do': 'make install' }
Jetpack 'posva/vim-vue' | let g:vue_pre_processors = []
Jetpack 'jwalton512/vim-blade'
Jetpack 'elzr/vim-json' | let g:vim_json_conceal = 0
Jetpack 'kevinoid/vim-jsonc'
Jetpack 'junegunn/vader.vim'
" }}}

" UI Jetpackins {{{
Jetpack 'Yggdroot/indentLine' | let g:indentLine_char = 'â”‚'
Jetpack 'machakann/vim-highlightedyank' | call cnull#highlightedyank#Setup()
Jetpack 'itchyny/lightline.vim' | call cnull#lightline#Setup()
Jetpack 'mengelbrecht/lightline-bufferline'
" }}}

" Colorschemes {{{
Jetpack 'bluz71/vim-nightfly-guicolors'
Jetpack 'bluz71/vim-moonfly-colors'
Jetpack 'fnune/base16-vim'
Jetpack 'haishanh/night-owl.vim'
" }}}

call jetpack#end()

" =============================================================================
" = UI/Theme (THEME) =
" =============================================================================

set background=dark
colorscheme night-owl
